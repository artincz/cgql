apply plugin: 'groovy'
apply plugin: 'eclipse'

group = 'cz.artin.cgql'
version = '1.0'

sourceCompatibility = 1.6
targetCompatibility = 1.6

sourceSets.main.resources.srcDirs = sourceSets.main.java.srcDirs + sourceSets.main.groovy.srcDirs

dependencies {
  groovy 'org.codehaus.groovy:groovy-all:2.0.0'

  compile 'commons-cli:commons-cli:1.2'
  compile 'jline:jline:2.7'
  compile 'com.google.guava:guava:11.0.1'
  compile 'commons-lang:commons-lang:2.6'

  compile fileTree(dir: 'lib', include: '*.jar')

}

task packageZip(type: Zip, dependsOn: jar) {
  into('lib') {
    from project.jar.archivePath
    from { project.configurations.runtime.files }
  }
  into('userlib') {
    from file('src/main/userlib/')
  }
  into('bin') {
    from (file('src/main/bin/')) {
      expand(project.properties)
      fileMode 0744
    }
  }
}

configurations {
  zipArchives
}

artifacts { zipArchives packageZip }

repositories {
  maven {
    url "http://builder/artifactory/libs-releases"
  }
}

eclipse {
  classpath {
    containers.remove('org.eclipse.jdt.launching.JRE_CONTAINER')
    containers "org.eclipse.jdt.launching.JRE_CONTAINER/org.eclipse.jdt.internal.debug.ui.launcher.StandardVMType/JavaSE-${sourceCompatibility}"
  }
}

task wrapper(type: Wrapper) {
  gradleVersion = '1.2'
}
